// Copyright (C) 2009, 2010, 2011, 2012, 1013, 2014 CNRS
// Authors:  Florent Lamiraux, Thomas Moulard.
//
// This file is part of the hpp-corbaserver.
//
// This software is provided "as is" without warranty of any kind,
// either expressed or implied, including but not limited to the
// implied warranties of fitness for a particular purpose.
//
// See the COPYING file for more information.

#ifndef HPP_CORBASERVER_ROBOT_SERVER_IDL
# define HPP_CORBASERVER_ROBOT_SERVER_IDL
# include <hpp/corbaserver/common.idl>

module hpp
{
  module corbaserver {
  /// Array of joint bounds.in order [v1_min,v1_max,v2_min,v2_max,...].
  typedef sequence<double> jointBoundSeq;

  /// Creation of a device, joints and bodies.
  interface Robot {
    ///  Load robot model
    ///
    /// \param robotName Name of the robot that is constructed,
    /// \param rootJointType type of root joint among "anchor", "freeflyer",
    /// "planar",
    /// \param packageName Name of the ROS package containing the model,
    /// \param modelName Name of the package containing the model
    /// \param urdfSuffix suffix for urdf file,
    ///
    /// The ros url are built as follows:
    /// "package://${packageName}/urdf/${modelName}${urdfSuffix}.urdf"
    /// "package://${packageName}/srdf/${modelName}${srdfSuffix}.srdf"
    ///
    void loadRobotModel (in string robotName, in string rootJointType,
			 in string packageName, in string modelName,
			 in string urdfSuffix, in string srdfSuffix)
      raises (Error);

    ///  Load humanoid robot model
    ///
    /// \param robotName Name of the robot that is constructed,
    /// \param rootJointType type of root joint among "anchor", "freeflyer",
    /// "planar",
    /// \param packageName Name of the ROS package containing the model,
    /// \param modelName Name of the package containing the model
    /// \param urdfSuffix suffix for urdf file,
    ///
    /// The ros url are built as follows:
    /// "package://${packageName}/urdf/${modelName}${urdfSuffix}.urdf"
    /// "package://${packageName}/srdf/${modelName}${srdfSuffix}.srdf"
    ///
    void loadHumanoidModel (in string robotName, in string rootJointType,
			    in string packageName, in string modelName,
			    in string urdfSuffix, in string srdfSuffix)
      raises (Error);

    /// \}

    /// \name Degrees of freedom
    /// \{

    /// Get size of configuration
    /// \param configSize size of configuration
    short getConfigSize () raises (Error);

    /// Get size of velocity
    /// \param numberDof size of velocity
    short getNumberDof () raises (Error);

    /// \}

    /// \name Joints
    /// \{

    /// Get joint names in the same order as in the configuration
    Names_t getJointNames () raises (Error);

    /// Get joint number degrees of freedom
    /// \param jointName name of the joint
    /// \throw Error if robot is not set or if joint does not exist.
    short getJointNumberDof (in string jointName) raises (Error);

    /// Get joint number config size
    /// \param jointName name of the joint
    /// \throw Error if robot is not set or if joint does not exist.
    short getJointConfigSize (in string jointName) raises (Error);

    ///  set a bound for the joint

    /// \param jointName name of the joint
    /// \param inJointBound sequence of joint dof bounds in order
    ///        [v0_min,v0_max,v1_min,v1_max,...].
    /// \li If vi_min > vi_max, dof of rank i is not bounded and bounds
    ///     (for random sampling) are set to [v_max, v_min].
    /// \li If size of sequence is different from twice the number of dofs,
    ///     no dof is bounded.
    void setJointBounds(in string jointName,
			in jointBoundSeq inJointBound)
      raises (Error);

    /// \}

    /// \name Configuration
    /// \{

    /// Get current configuration
    /// \return dofArray Array of degrees of freedom.
    floatSeq getCurrentConfig() raises (Error);

    /// Set current configuration of specified robot,
    /// \param dofArray Array of degrees of freedom /
    void setCurrentConfig(in floatSeq dofArray) raises (Error);

    /// \}

    /// \name Bodies
    /// \{

    ///  Get the list of objects attached to a joint.
    /// \param inJointName name of the joint.
    /// \return list of names of CollisionObject attached to the body.
    Names_t getJointInnerObjects (in string jointName) raises (Error);


    ///  Get list of collision objects tested with the body attached to a joint
    /// \param inJointName name of the joint.
    /// \return list of names of CollisionObject
    Names_t getJointOuterObjects (in string jointName) raises (Error);

    /// \}

    /// \name Collision checking and distance computation
    /// \{

    /// Test collision with obstacles and auto-collision.
    ///
    /// Check whether current configuration of robot is valid by calling
    /// CkwsDevice::collisionTest ().
    /// \retval validity whether configuration is valid
    void collisionTest (out boolean validity) raises (Error);

    /// Compute distances between bodies and obstacles
    ///
    /// \retval distances list of distances,
    /// \retval innerObjects names of the objects belonging to a body
    /// \retval outerObjects names of the objects tested with inner objects,
    /// \retval innerPoints closest points on the body,
    /// \retval outerPoints closest points on the obstacles
    /// \note outer objects for a body can also be inner objects of another
    /// body.
    void distancesToCollision (out floatSeq distances,
			       out Names_t innerObjects,
			       out Names_t outerObjects,
			       out floatSeqSeq innerPoints,
			       out floatSeqSeq outerPoints) raises (Error);

    /// \}

    /// \name Mass and inertia
    /// \{

    /// Get mass of robot
    double getMass () raises (Error);

    /// Get position of center of mass
    floatSeq getCenterOfMass () raises (Error);

    /// Get Jacobian of the center of mass
    floatSeqSeq getJacobianCenterOfMass () raises (Error);

    /// \}

    /// \name Create and register robot
    /// \{

    /// Create an empty device and store it temporarily
    /// \param robotName name of the robot.
    ///  Fails if another robot is already in construction.
    void createRobot(in string robotName) raises (Error);

    /// Create a new joint
    /// \param jointName name of the joint,
    /// \param jointType among ["anchor", "SO3", "rotation",
    ///                         "translation"]
    /// \param pos initial position of the joint
    /// \param joinBounds bounds of the joint. See setJointBounds for details.
    void createJoint(in string jointName, in string jointType,
		     in Transform pos, in jointBoundSeq jointBounds)
      raises (Error);

    ///  Add a child joint to a joint.
    /// \param inParentName name of the joint to which a child is added.
    /// \param inChildName name of the child joint added to the previous one.
    /// \throw Error.
    void addJoint(in string inParentName, in string inChildName)
      raises (Error);

    /// Add a robot to the ProblemSolver
    /// \throw Error.
    void setRobot (in string inRobotName) raises (Error);

    ///  Set a joint as root joint to a robot.
    /// \param inRobotName name of the robot (ChppDevice).
    /// \param inJointName name of the joint.
    void setRobotRootJoint(in string inRobotName, in string inJointName)
      raises (Error);

    ///  create an empty polyhedron.
    /// \param inPolyName name of the polyhedron.
    /// \throw Error.
    void createPolyhedron(in string inPolyName) raises (Error);

    ///  Create a box
    /// \param name name of the box
    /// \param x, y, z Size of the box
    void createBox (in string name, in double x, in double y, in double z)
      raises (Error);

    ///  Create a sphere
    /// \param name name of the sphere
    /// \param radius radius of the sphere
    void createSphere (in string name, in double radius) raises (Error);

    ///  Add a point to a polyhedron
    /// \param inPolyName the name of the polyhedron.
    /// \param x coordinate of the point.
    /// \param y coordinate of the point.
    /// \param z coordinate of the point.
    /// \return rank of point in polyhedron
    short addPoint(in string inPolyName, in double x, in double y, in double z)
      raises (Error);

    ///  Add a point to a polyhedron
    /// \param inPolyName the name of the polyhedron.
    /// \param pt1 rank of first point in polyhedron.
    /// \param pt2 rank of second point in polyhedron.
    /// \param pt3 rank of third point in polyhedron.
    /// \return rank of triangle in polyhedron
    short addTriangle(in string inPolyName, in unsigned long pt1,
		      in unsigned long pt2, in unsigned long pt3)
      raises (Error);

    ///  Attach an object to a joint.
    /// \param jointName name of the body
    /// \param objectName name of the object
    /// \param pos relative position of the polyhedron in the body
    void addObjectToJoint(in string jointName, in string objectName,
			  in Transform pos) raises (Error);

    ///  Compute Projected Volume of the robot body
    /// \return position of capsules
    floatSeq computeVolume() raises (Error);

    ///  Parse points of capsules attached to the robot body
    /// \return 2 points per capsules each containing (x,y,z,radius,length)
    ///         and arranged in one flat 1d array
    floatSeq parseCapsulePoints () raises (Error);


    floatSeq projectConfigurationUntilIrreducible (in floatSeq dofArray) raises (Error);
    floatSeq gradientConfigurationWrtProjection(in floatSeq dofArray) raises (Error);
    floatSeq getRandomConfig() raises (Error);

    /// \}
  };
  };
};

#endif // HPP_CORBASERVER_ROBOT_SERVER_IDL
